Koolex

high

# Non-negligible precision loss for tokens that have small decimals

## Summary
Non-negligible precision loss in `ratePerSecond()` function for tokens that have less than 6 decimals such as GUSD (Gemini dollar). 

## Vulnerability Detail
Let's assume:
1. Stream amount = 1M, 
2. Duration = 31_557_600 seconds (1 year)

We would have
- ratePerSecondWithoutLoss = 1000000 / 31_557_600 = 0.031688087814029
- Max round down impact = duration - 1 = 31_557_599


Let's apply the math on 3 tokens USDC, GUSD and WETH

- **USDC (6 decimals):**
            `ratePerSecond()` = 0.031688 
            lossPerSecond = ratePerSecondWithoutLoss - ratePerSecond
            lossPerSecond = 0.000000087814029
            lossPer(Year-1) = 31_557_599 * lossPerSecond = 2.771199912185947 USDC which is negligible

- **GUSD (2 decimals):**
            `ratePerSecond()` = 0.03 since 
            lossPerSecond = ratePerSecondWithoutLoss - ratePerSecond
            lossPerSecond = 0.001688087814029
            lossPer(Year-1) = 31_557_599 * lossPerSecond = 53271.998311913756371 GUSD which is not negligible
  
- **WETH (18 decimals):**
            `ratePerSecond()` =  0.031688087814028950 
            lossPerSecond = ratePerSecondWithoutLoss - ratePerSecond
            lossPerSecond = 0,000000000000000050
            lossPer(Year-1) = 31_557_599 * lossPerSecond = 0.00000000157788 WETH which is negligible

As you can see, the ratePerSecond calculation results in non-negligible loss if the token for example has 2 decimals unlike tokens with 6 or 18 decimals. As per the sponsor, the contract should support popular ERC20 tokens.

## Impact
- Less balance per second for the recipient.
- The recipient has to wait till the stream ends to claim the remainder.
- In case the stream is cancelled, the recipient receives less than their fair share of the funds.

## Code Snippet

-  `ratePerSecond()`
	https://github.com/sherlock-audit/2022-11-nounsdao/blob/main/src/Stream.sol#L125-L138

- PoC:

	- Create a file called **StreamDecimals.t.sol** under **test** directory and add the following code:

```sh
pragma solidity ^0.8.17;

import "forge-std/Test.sol";
import { ERC20Mock } from "openzeppelin-contracts/mocks/ERC20Mock.sol";
import { StreamFactory } from "../src/StreamFactory.sol";
import { Stream } from "../src/Stream.sol";
import { IStream } from "../src/IStream.sol";

contract StreamTest is Test {
    uint256 constant DURATION = 1000;
    uint256 constant STREAM_AMOUNT = 2000;

    uint256 startTime;
    uint256 stopTime;
    address payer = address(0x11);
    address recipient = address(0x22);

    event TokensWithdrawn(address indexed msgSender, address indexed recipient, uint256 amount);

    event StreamCancelled(
        address indexed msgSender,
        address indexed payer,
        address indexed recipient,
        uint256 payerBalance,
        uint256 recipientBalance
    );

    ERC20Mock token;
    Stream s;
    StreamFactory factory;

    function setUp() public virtual {
        token = new ERC20Mock("Mock Token", "MOCK", address(1), 0);
        factory = new StreamFactory(address(new Stream()));

        startTime = block.timestamp;
        stopTime = block.timestamp + DURATION;
    }
}


contract StreamWithLowDecimals is StreamTest {
    function setUp() public override {
        super.setUp();
    }
     function test_ratePerSecond_USDC() public {
        uint256 streamAmount = 1_000_000 * 1e6; // 1M USDC 
        uint256 duration = 31_557_600; // 365.25 days

        startTime = block.timestamp;
        stopTime = startTime + duration;

        s = Stream(
            factory.createStream(
                payer, recipient, streamAmount, address(token), startTime, stopTime
            )
        );

        // USDC
        // streamAmount/duration = 0.031688087814029
        // after one second
        vm.warp(startTime+1); 
        console.log('USDC Recipient Balance:  %d = 0.031688',s.balanceOf(recipient)); // 31688 = 0.031688 USDC
    }

    function test_ratePerSecond_GUSD() public {
        uint256 streamAmount = 1_000_000 * 1e2; // 1M GUSD 
        uint256 duration = 31_557_600; // 365.25 days

        startTime = block.timestamp;
        stopTime = startTime + duration;

        s = Stream(
            factory.createStream(
                payer, recipient, streamAmount, address(token), startTime, stopTime
            )
        );

        // GUSD
        // streamAmount/duration = 0.031688087814029
        // after one second
        vm.warp(startTime+1); 
        console.log('GUSD Recipient Balance:  %d = 0.03',s.balanceOf(recipient)); // 3 = 0.03 GUSD
    }

    function test_ratePerSecond_WETH() public {
        uint256 streamAmount = 1_000_000 * 1e18; // 1M WETH 
        uint256 duration = 31_557_600; // 365.25 days

        startTime = block.timestamp;
        stopTime = startTime + duration;

        s = Stream(
            factory.createStream(
                payer, recipient, streamAmount, address(token), startTime, stopTime
            )
        );

        // WETH
        // streamAmount/duration = 0.031688087814029
        // after one second
        vm.warp(startTime+1); 
        console.log('WETH Recipient Balance:  %d = 0.031688087814028950',s.balanceOf(recipient)); // 31688087814028950 = 0.031688087814028950 WETH
    }

}

```


Then run:
```sh
forge test --match-path test/StreamDecimals.t.sol -vv
```

You should get the following logs on the console:

```sh
[PASS] test_ratePerSecond_GUSD() (gas: 167284)
Logs:
  GUSD Recipient Balance:  3 = 0.03

[PASS] test_ratePerSecond_USDC() (gas: 167294)
Logs:
  USDC Recipient Balance:  31688 = 0.031688

[PASS] test_ratePerSecond_WETH() (gas: 167339)
Logs:
  WETH Recipient Balance:  31688087814028950 = 0.031688087814028950
  ```

## Tool used

Manual Review

## Recommendation

Consider normalizing the token to 18 decimals upon initializing the stream.